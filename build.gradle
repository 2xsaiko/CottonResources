plugins {
	id 'fabric-loom' version '0.2.3-SNAPSHOT'
	id 'maven-publish'
	id 'com.jfrog.artifactory' version '4.9.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "http://server.bbkr.space:8081/artifactory/libs-release/" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modCompile "io.github.cottonmc:cotton:${project.cotton_version}"
	modCompile "io.github.cottonmc:Jankson:${project.jankson_version}"
	include "io.github.cottonmc:cotton:${project.cotton_version}"
	include "io.github.cottonmc:Jankson:${project.jankson_version}"
	implementation "com.google.code.findbugs:jsr305:3.0.2"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(jar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}
}

if (rootProject.file('private.gradle').exists()) {
	apply(from: 'private.gradle')
}

artifactory {
	if (project.hasProperty("artifactoryUsername")) {
		contextUrl = 'http://server.bbkr.space:8081/artifactory/libs-release/'
		publish {
			repository {
				username = artifactoryUsername
				password = artifactoryPassword
			}
			defaults {
				publications('maven')

				publishArtifacts = true
				publishPom = true
			}
		}
	} else {
		println "Missing Artifactory username (and password)"
	}
}
